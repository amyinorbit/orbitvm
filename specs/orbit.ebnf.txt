// Orbit Grammar (EBNF-ish).
// copyright (c) 2017 Amy Parent <amy@amyparent.com>

program         ::= (decl)*
decl            ::= type-decl
                  | var-decl
                  | func-decl
                  
// Type Declarations

type-decl       ::= 'type' identifier type-block
type-block      ::= '{' (var-decl)+ '}'

// Variable Declarations

var-decl        ::= 'var' identifier ((':', type) | ((':', type)? '=' expression))

// Function Declarations

func-decl       ::= func-proto '{' block '}'
func-proto      ::= 'func' identifier parameters '->' type
parameters      ::= '(' (parameter-list)? ')'
parameter-list  ::= formal-param (',' formal-param)*
parameter       ::= identifier ':' type

// Statements

block           ::= '{' (statement)* '}'
statement       ::= conditional
                  | expression

conditional     ::= if-statement
                  | while-loop
                  | for-loop
if-statement    ::= 'if' expression block ('else' statement)?
while-loop      ::= 'while' expression block
for-loop        ::= 'for' identifier 'in' expression block

// Expressions - Variable assignments (compound or not) are expressions

expression      ::= unary (binary-op unary) *
unary           ::= (unary-op)? term
term            ::= literal
                  | name-or-call
                  | '(' expression ')'
name-or-call    ::= identifier (suffix)*
suffix          ::= (subscript | func-call | field-access)?
field-access    ::= '.' name-or-call                        // Orbit doesn't have member functions, but has first-order functions
subscript       ::= identifier '[' expression ']'
func-call       ::= '(' (expression-list)? ')'
expression-list ::= expression (',' expression)*

binary-op       ::= '+'
                  | '-'
                  | '/'
                  | '*'
                  | '%'
                  | '**'
                  | '^'
                  | '|'
                  | '&'
                  | '>>'
                  | '<<'
                  | '<'
                  | '>'
                  | '<='
                  | '>='
                  | '=='
                  | '&&'
                  | '||'
                  | '!='
                  | '='
                  | '+='
                  | '-='
                  | '*='
                  | '/='
unary-op        ::= '!'
                  | '-'
                  | '~'

// Type System

type            ::= ('maybe')? typename
typename        ::= primitive
                  | array-type
                  | map-type
                  | user-type
primitive       ::= 'Nil'
                  | 'Number'
                  | 'Boolean'
                  | 'String'
                  | 'Any'
array-type      ::= 'Array' '[' type ']'
map-type        ::= 'Map' '[' type ':' type ']'
user-type       ::= identifier


// Literals

number          ::= float-literal
                  | integer-literal
float-literal   ::= fractional
                  | exponential
integer-literal ::= sign decimal
fractional      ::= sign decimal '.' decimal
exponential     ::= sign decimal 'e' sign decimal

decimal         ::= [0-9]+
sign            ::= ('+' | '-')?
string-literal = \" [^\r\n\\\"]* \"

